extern func printf(str s, ...) : int;

define struct Value {int v;}
define enum OptVal {Unit, Value}

define Database :: c : Channel<
                        !ExternalChoice<
                              get: +int;-OptVal,
                              set: +int;+Value,
                              lock: +int;InternalChoice<
                                       present: -Value;+Value,
                                       missing: +Value>>> = {

  Value[10] data; 

  define func PrintDatabase(Value[10] data) {
      var i := 0; 
      while i < data.length {
        match data[i]
          | Value v => printf("%u, ", v.v);
          | Unit u => printf("*, ");
        i := i + 1; 
      }
      printf("\n");
      return;
  }

  printf("Initial Database State:\n");
  PrintDatabase(data); 

  accept(c) {
    offer c 
      | get => { 
          c.send(data[c.recv()]);
        }
      | set => {
            data[c.recv()] := c.recv();
        }
      | lock => {
          var k := c.recv();
          c[missing]
          data[k] := c.recv();
        }
    printf("Database Updated: \n");
    PrintDatabase(data); 
  }
}

define program :: c : Channel<-int> = {
    var db := exec Database;
    var rqs := exec requests;
    var setRq := exec writeRequest; 


    accept(rqs) {
      acceptIf(setRq, setRq.is_present()) {
        printf("Acceptif Present!\n");
        more(db); 
        db[set]
        db.send(setRq.recv())
        db.send(setRq.recv())
      }
      else 
      {
        printf("AcceptIf Not Present!\n");
      }
   
      more(db)
      offer rqs 
        | +int;-OptVal => { 
            db[get] 
            db.send(rqs.recv())  
            var a := db.recv();
            rqs.send(a) 
          } 
        | +int;+Value => { 
            db[set] 
            db.send(rqs.recv()) 
            db.send(rqs.recv()) 
          }
        | +int;InternalChoice<-Value;+Value, +Value> => { 
            printf("72\n"); 
            db[lock] 
            db.send(rqs.recv())
            offer db 
                | present => { rqs[-Value;+Value] rqs.send(db.recv()) db.send(rqs.recv())}
                | missing => { rqs[+Value] db.send(rqs.recv()) }
          }
  
    }
 

    accept(setRq) {
      more(db); 
      db[set]
      db.send(setRq.recv())
      db.send(setRq.recv())
    }

    weaken(db)
    c.send(0);
}


define requests :: c : Channel<
                        ?InternalChoice<
                              -int;+OptVal,
                              -int;-Value,
                              -int;ExternalChoice<
                                       +Value;-Value,
                                       -Value>>> = {

    
    more(c)
    c[-int;+OptVal]
    c.send(4)
    var opt := c.recv(); 

    printf("Read Request for 4 got: ");

    match opt
      | Unit e => { printf("empty\n"); }
      | Value v => { printf("%u\n", v.v); }

    more(c)
    c[-int;+OptVal]
    c.send(20)
    opt := c.recv(); 

    printf("Read Request for 20 got: ");

    match opt
      | Unit e => { printf("empty\n"); }
      | Value v => { printf("%u\n", v.v); }

    weaken(c)
}

define writeRequest :: c : Channel<?(-int;-Value)> = {
    more(c)
    c.send(4)
    c.send(Value::init(2))
    
    weaken(c)
}